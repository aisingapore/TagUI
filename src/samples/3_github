https://github.com/tebelorg/Tmail
// automation flow files start with an URL to tell TagUI where to go

// this flow goes to a GitHub page and downloads the repo using 2 ways
// for issues or questions, kindly feedback on GitHub or ken@tebel.org
// see cheatsheet for steps, conditions, finding element identifiers, etc
// https://github.com/tebelorg/TagUI#cheat-sheet

// use read (or fetch) step to fetch webpage element text to variable
fetch (//*[@class="numbers-summary"]/li)[5] to license_type

// use check step to check a condition and print result to output
// text to put in quotation marks to differentiate from variables
check license_type equals to "MIT" | "Yes, repo is under MIT license." | "No, repo is not under MIT license."  

// for headless and visible Chrome, file can be downloaded using normal webpage interaction
// for eg, various clicking on webpage as a normal user would to trigger the file download

// for Firefox and PhantomJS mode, file can be downloaded using download or receive step
// some websites including GitHub may detect and block automated downloads
// so below download step may not work to download the file correctly

// use download step to proactively download a file directly from an URL
download https://github.com/tebelorg/Tmail/archive/master.zip to download.zip

// use receive step for event-based download when an URL accessed contains your keyword
receive archive/master.zip to receive.zip

// as receive step is event-based, need to interact with the webpage to trigger download
// for headless and visible Chrome, file can be downloaded with this webpage interaction
click Clone or download
click Download ZIP

// wait for a few seconds to make sure download has time to complete
wait 10 seconds

// use api step to call api, response will be saved in api_result variable
// variables can be used (eg from fetch or read step, files or self-declared)
// use '+variable_name+' to incorporate variables into your api call 
var country = "SG"
api http://services.groupkt.com/country/get/iso2code/'+country+'
echo api_result
echo 'Country name - ' + api_json.RestResponse.result.name
